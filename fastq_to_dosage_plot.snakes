import re
from Bio import SeqIO
import pandas as pd
shell.executable('bash')

configfile: "config.yaml"

units = pd.read_csv(config["units"], index_col=["sample", "unit"], dtype=str, sep="\t")
units.index = units.index.set_levels([i.astype(str) for i in units.index.levels])

# replace fofn system with simple Python dict
samples = {}
for i in units.index.levels[0]:
    samples[i] = ["data/dedup/{}-{}.bam".format(i,j) for j in units.loc[i].index]

def is_single_end(sample,unit):
    return pd.isnull(units.loc[(sample, unit), "fq2"])

def get_fastq(wildcards):
    return "data/reads/"+units.loc[(wildcards.sample, wildcards.unit), ['fq1', 'fq2']].dropna()

def get_fastq_test(wildcards):
    tmp = units.loc[units['parhap'] != 'control']
    return "data/reads/"+tmp.loc[(wildcards.sample,wildcards.unit), ['fq1','fq2']].dropna()

def get_fastq_control(wildcards):
    tmp = units.loc[units['parhap'] == 'control']
    return "data/reads/"+tmp.loc[(wildcards.sample,wildcards.unit), ['fq1','fq2']].dropna()

def get_trimmed(wildcards):
    if not is_single_end(**wildcards):
        # paired-end sample should be aligned as such
        return expand("data/trimmed/{sample}-{unit}-{group}.fastq.gz",
            group=[1,2], **wildcards)
    # single end sample
    return "data/trimmed/{sample}-{unit}.fastq.gz".format(**wildcards)

rule all:
    input:
        config["genome"],
        config["genome"]+".bwt",
        config["genome"]+".fai",
        re.sub("(\.fasta$|\.fa$)", ".genome", config["genome"]),
        re.sub("(\.fasta$|\.fa$)", "_windows.bed", config["genome"]),
        ["data/plots/{}-dosage_plot.pdf".format(i[0]) for i in units.index]

include: "rules/dosage_plots.rules"
include: "rules/bedtools_coverage.rules"
include: "rules/samtools_index.rules"
include: "rules/bam_mapqual_filter.rules"
include: "rules/samtools_merge.rules"
include: "rules/mark_duplicates.rules"
include: "rules/align.rules"
include: "rules/cutadapt.rules"
include: "rules/cutadapt_hardtrim.rules"
include: "rules/get_SRA_reads.rules"
include: "rules/get_genome.rules"
include: "rules/clean_genomes.rules"
include: "rules/combine_genomes.rules"
include: "rules/bwa_index.rules"
include: "rules/samtools_faidx.rules"
include: "rules/bedtools_genome_windows.rules"